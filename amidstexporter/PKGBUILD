# Maintainer: Frederik "Freso" S. Olesen <archlinux@freso.dk>

pkgname=amidst
pkgver='3.7'
_jarver="${pkgver//_/-}"
_jarfile="${pkgname^^}-$_jarver.jar"
pkgrel=1
pkgdesc='Advanced Minecraft Interface and Data/Structure Tracking'
arch=('any')
license=('GPL3')
url='http://www.minecraftforum.net/topic/626786-'
depends=('java-runtime')
optdepends=('minecraft: the game itself')
noextract=("$_jarfile")
changelog=ChangeLog
source=("https://github.com/skiphs/AMIDST/releases/download/$_jarver/$_jarfile"
        amidst.sh
        amidst.desktop)
md5sums=('be52f1eaf644fca9c95e1fbe7e2ee5dc'
         '783c4040c9736f96ce72e9997833a0bf'
         '3c6900ac68e3175768322e684f9f1bcb')

prepare() {
    cd "$srcdir"

    # Extract icon
    bsdcpio --extract --make-directories --insecure 'amidst/resources/icon.png' < "$_jarfile"
}

package() {
    cd "$srcdir"

    install -vDm755 'amidst.sh'                 "$pkgdir/usr/bin/amidst"
    install -vDm644 'amidst/resources/icon.png' "$pkgdir/usr/share/pixmaps/amidst.png"
    install -vDm644 'amidst.desktop'            "$pkgdir/usr/share/applications/amidst.desktop"
    install -vDm644 "$_jarfile"                 "$pkgdir/usr/share/java/$pkgname/AMIDST.jar"
}

# vim:set ts=4 sw=4 et:
