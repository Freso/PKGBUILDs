pkgname=picard-git
pkgver=20120505
pkgrel=2
pkgdesc="MusicBrainz tagging application"
arch=('i686' 'x86_64')
url="http://musicbrainz.org/doc/MusicBrainz_Picard"
license=('GPL')
depends=('python2-pyqt' 'mutagen' 'libofa' 'ffmpeg')
optdepends=('libdiscid: CD-Lookup feature'
            'chromaprint: AcoustID integration')
makedepends=('git')
provides=('picard')
conflicts=('picard' 'picard-qt' 'picard-bzr')

_gitroot=https://github.com/musicbrainz/picard.git
_gitname=picard

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
      cd "$_gitname" && git pull origin
      msg "The local files are updated."
    else
      git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

    python2 setup.py config
    python2 setup.py build
}

package() {
    cd "$srcdir/$_gitname-build"
    python2 setup.py install --root=$pkgdir
}
