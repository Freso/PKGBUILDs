# Maintainer: Frederik "Freso" S. Olesen <archlinux@freso.dk>
_pkgname=chunky
pkgname=${_pkgname}-git
pkgver=1.1.2
pkgrel=1
pkgdesc='A Minecraft mapping and rendering tool.'
arch=('any')
url='http://chunky.llbit.se/'
license=('GPL3')
depends=('java-runtime')
makedepends=('git' 'apache-ant' 'java-environment')
optdepends=(
  'minecraft: The game itself.'
)
provides=('chunky')
changelog=ChangeLog
source=(
  "git+https://github.com/llbit/${_pkgname}.git"
  "chunky.desktop"
)
md5sums=('SKIP'
         '88d8c8cca805047ce10e20774b6cbc8a')

pkgver() {
  cd "$srcdir/chunky"
  git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/chunky"
  ant jar
  
  cp Chunky.sh ../
  sed -i "s|Chunky\.jar|/usr/share/java/$_pkgname/Chunky\.jar|" ../Chunky.sh
}

package() {
  cd "$srcdir/chunky"

  install -d "$pkgdir/usr/share/doc/$_pkgname"
  install -m644 -t "$pkgdir/usr/share/doc/$_pkgname" ./*.txt Chunky.bat Chunky.sh

  install -vDm644 'build/Chunky.jar'     "$pkgdir/usr/share/java/$_pkgname/Chunky.jar"
  install -vDm755 '../Chunky.sh'         "$pkgdir/usr/bin/chunky"
  install -vDm644 'bin/chunky.png'       "$pkgdir/usr/share/pixmaps/chunky.png"
  install -vDm644 '../chunky.desktop'    "$pkgdir/usr/share/applications/chunky.desktop"
}

# vim:set ts=2 sw=2 et:
